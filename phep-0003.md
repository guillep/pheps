```
PhEP {
	#id: 0003,
	#type: #image,
	#title: 'New Finalization',
	#status: #accepted
	#authors: [ 'Guillermo Polito' ],
	#created: '2023-01-09'
}
```

# Abstract 

A proposal describing a new finalization mechanism for Pharo with the following properties:

 - API independent from its implementation
 - Avoids memory leaks

This document describes both a finalization API as a Pharo library and an efficient virtual machine implementation through Ephemerons.

# Changelog

- 2023-03-09: Pass on comments and Assigned Phep ID
- 2023-01-09: Initial Version

# Motivation

Proper resource management should be correctly taken into account when designing and programming software and API.
This means some applications need to track when objects are *about to* be garbage collected to perform some treatment. For example, close a file/socket when the corresponding file/socket object is garbage-collected.
Such a mechanism is implemented through automatic finalization.

The finalization mechanism present in Pharo up to version 10 (inclusive) is suboptimal.
First, it requires a full scan of many data structures to detect finalization candidates.
Second, the bad usage of the finalization mechanism leads to memory leaks.
Third, the current API is tied to its implementation (`WeakRegistry`).

# Description

## Terminology

- **Finalizable**: an object of interest regarding garbage collection. When this object is about to be garbage-collected we want to notify a **finalizer**.
- **Finalizer**: an object that gets notified when a **finalizable** is about to be garbage-collected to perform some action.
- **Finalization Registry**: a registry of *(finalizable, finalizer)* pairs containing finalizable objects and their corresponding finalizer.

## Finalization Overview and Protocol

Finalization is managed at the user level by a finalization registry.
A finalization registry contains (finalizable, finalizer) pairs.
A registry has two main methods: `#add:` and `#add:finalizer:`.
Finalizers need to implement `#finalize` with their corresponding finalization

- `FinalizationRegistry>>#add:` adds an object as both finalizable and finalizer. This means the object is its own finalizer.
- `FinalizationRegistry>>#remove:` removes an object as both finalizable and finalizer. This means the object is its own finalizer.
- `FinalizationRegistry>>#add:finalizer:` adds an object as finalizable with another object as a finalizer.
- `[User Object]>>#finalize` invokes the finalization mechanism of the given finalizer. The default `#finalize` method in `Object` is defined to do nothing. 

### Examples

The following example shows how to set a pair (finalizable, finalizer).

```smalltalk
"Create a finalizable object implementing #finalize"
MyFinalizer << Object
	package: 'MyPackage'.
MyFinalizer >> #finalize [
	'Gone!' traceCr.
]

"Add it to a registry"
objectOfInterest := Object new.
registry := FinalizationRegistry new.

"Object is different from the finalizer"
registry add: objectOfInterest finalizer: MyFinalizer new.

"Make the object collectable and see the transcript"
objectOfInterest := nil.
"Transcript:
  Gone!"
```


The following example shows how an object can be its own finalizer.

```smalltalk
"Create a finalizable object implementing #finalize"
MyObject << Object
	package: 'MyPackage'.
MyObject >> #finalize [
	'Gone!' traceCr.
]

"Add it to a registry"
objectOfInterest := MyObject new.
registry := FinalizationRegistry new.

"Object is its own finalizer"
registry add: objectOfInterest.

"Make the object collectable and see the transcript"
objectOfInterest := nil.
"Transcript:
  Gone!"
```

### Management of Registries

The following invariants of a registry must be taken into account: 

- Many registry instance can co-exist;
- Registry instances need to be stored by users to prevent their collection.

There are no restrictions on how many finalizers exist at runtime.
Applications can instantiate their own finalization registry, for example, to keep their domain-specific finalizers separate.
By default, the proposed implementation provides a globally visible registry as a singleton:

```smalltalk
FinalizationRegistry class >> default [

	^ Default ifNil: [ Default := self new ]
]
```


Moreover, notice that finalization happens only if a registry is live.
If a finalization registry is collected, the relation between (finalizable, finalizer) pairs is lost, and finalization cannot take place.
It's the user's responsibility to hold their own finalization registry strongly by their applications.


### Strong and Weak references in the Registry

The following invariants of objects registered for finalization must be taken into account: 

- it does not prevent the garbage collection of its finalizables;
- it does prevent the garbage collection of its registered finalizers;

As shown in the examples above, the finalization registry holds a reference to the finalizable object and the finalizer object.
On the one hand, the finalizer is strongly held, and it will not be garbage collected until its finalizable object is collected too.
On the other hand, the registry does not prevent the GC to identify finalizable objects as collectable.
In practice, a registry's finalizables are referenced weakly although, instead of putting a tombstone for the reference, the corresponding finalizer is invoked.

## On the Relation between Finalizables and Finalizers

### Sharing Finalizers

The current proposal puts no constraints on the sharing of finalizers.
This means that the same finalizer could be shared in many subscriptions.
If many finalizables share the same finalizer, the finalizer should be idempotent.
It is the user's responsibility to make sure that their finalizers are shareable.

One special case is that the same pair (finalizable, finalizer) can be subscribed many times in a registry.
The current proposal does not add any restrictions on such behaviour.
It is up to the user to ensure that calling multiple times the same finalizer for the same object yields the expected (application dependent) behaviour.

### Order of Evaluation

The current proposal puts no constraints on how many finalizers can be subscribed for a single object, meaning one object can have several finalizers.
If two finalizers are subscribed for an object (say `f1` and `f2`), the question of the finalization order appears.
Indeed, under certain conditions and with side effects in mind the order `(f1,f2)` could yield different results than `(f2, f1)`.
The order of evaluation of finalizers remains (explicitly) **undefined for this iteration of the proposal**, and will be implementation defined.
This means that users of finalization should make sure that finalisers can be executed in any order.

### About Object Revival
It is possible, although not recommended, to *revive* a finalizable object during finalization.
When a finalizer is notified, it means its corresponding finalizable is a candidate for collection.
In such case, it is possible from the `#finalize` method to take the finalizable object and subscribe it some a global reference.

Once an object is revived from a finalizer, this same finalizer will become inactive. This means that in the future the revived object becomes candidate for collection once again, it will not be finalized again *unless* another finalizer for it is subscribed in the registry.

## Ephemerons

The proposed implementation makes use of ephemerons.
This section briefly explains what are ephemerons and how they are used for finalization.
For more details, check out the paper.

> Barry Hayes. 1997. Ephemerons: a new finalization mechanism. SIGPLAN Not. 32, 10 (Oct. 1997), 176â€“183. https://doi.org/10.1145/263700.263733

Ephemerons are key-value pairs that are treated specially by the garbage collector, where

 - the key is the finalizable object
 - the value is any other object whose life-cycle is associated to the key

The garbage collector detects when an ephemeron key is a candidate for collection, in which case it puts the ephemerons in a queue for later finalization.
The ephemeron implementation guarantees that strong references from the ephemeron's value do not prevent the key's collection, avoiding memory leaks.

The finalization library consumes the objects from the queue of ephemerons and sends the message *#mourn* to each of them. `#mourn` will remove the subscription from the corresponding finalization registry and send the message `#finalize` to its value -- the finalizer.

## Implementation Details

The current proposal is accompanied by an implementation in form of pull requests.
This section presents the proposal and describes some implementation details.

### Implementation Proposal

- **Library Pharo PR:** The following PR introduces in Pharo 11 the API described above.

[https://github.com/pharo-project/pharo/pull/12042/files](https://github.com/pharo-project/pharo/pull/12042/files)

- **VM support:** The support for Ephemerons was started a long time ago by Eliot Miranda et al. A great effort has been done in the previous months to improve its robustness. The latest version is already available in branch `pharo-10` of https://github.com/pharo-project/pharo-vm, with latest integrated PR being [https://github.com/pharo-project/pharo-vm/pull/511](https://github.com/pharo-project/pharo-vm/pull/511)

### Threading and Error Management

The library side of finalization will consume ephemerons from the queue using a separate green thread. This process will consume ephemerons one by one and send them the `#mourn` message.
If an error happens while finalizing an object, a new process will consume the rest of the ephemerons.

### Removals from Pharo >= 10 and Backwards Compatibility

We propose:

 - **Backwards compatibility:** to deprecate `WeakRegistry` and replace its usages with `FinalizationRegistry`. This will keep backwards compatibility for users of finalization.
 - **Cleanups:** To replace the internals of `WeakRegistry` and use a `FinalizationRegistry` instead. This will allow to remove the old finalization implementation without affecting its users.

 
